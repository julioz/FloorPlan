plugins {
    id 'java'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id "com.gradle.plugin-publish" version "0.12.0"
    id 'signing'
}

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

def isReleaseBuild = !version.toString().endsWith("SNAPSHOT")

gradlePlugin {
    plugins {
        floorplan {
            id = "com.juliozynger.floorplan"
            displayName = "FloorPlan"
            description = "Render database schemas into ER diagrams"
            implementationClass = 'com.zynger.floorplan.FloorPlanPlugin'
        }
    }
}

// For publishing to the Gradle Plugin Portal
// https://plugins.gradle.org/docs/publish-plugin
pluginBundle {
    website = "https://github.com/julioz/FloorPlan"
    vcsUrl = "https://github.com/julioz/FloorPlan"
    tags = ["room", "diagram", "er", "graphviz", "android", "floorplan"]

    mavenCoordinates {
        artifactId = "floorplan-gradle-plugin"
        groupId = project.group.toString()
    }
}

// For publishing to other repositories
publishing {
    repositories {
        maven {
            name = "sonatype"

            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            if (isReleaseBuild) {
                url = releasesRepoUrl
            } else {
                url = snapshotsRepoUrl
            }
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
    afterEvaluate {
        publications {
            withType(MavenPublication) { publication ->
                signing.sign(publication)
                // customize all publications here
                pom {
                    description = "Render database schemas into ER diagrams"
                    name = "FloorPlan"
                    url = "https://github.com/julioz/FloorPlan"
                    issueManagement {
                        system = "GitHub"
                        url = "https://github.com/julioz/FloorPlan/issues"
                    }
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    scm {
                        url = "https://github.com/julioz/FloorPlan"
                        connection = "scm:git:git://github.com/julioz/FloorPlan.git"
                        developerConnection = "scm:git:ssh://github.com/julioz/FloorPlan.git"
                    }
                    developers {
                        developer {
                            id = "julioz"
                            name = "Julio Zynger"
                        }
                    }
                }
            }
        }
    }
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifact sourcesJar
            artifact javadocJar

            pom {
                description = "Render database schemas into ER diagrams"
                name = "FloorPlan"
                url = "https://github.com/julioz/FloorPlan"
                issueManagement {
                    system = "GitHub"
                    url = "https://github.com/julioz/FloorPlan/issues"
                }
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                scm {
                    url = "https://github.com/julioz/FloorPlan"
                    connection = "scm:git:git://github.com/julioz/FloorPlan.git"
                    developerConnection = "scm:git:ssh://github.com/julioz/FloorPlan.git"
                }
                developers {
                    developer {
                        id = "julioz"
                        name = "Julio Zynger"
                    }
                }
            }
        }
    }
}

shadowJar {
    minimize {
        /**
         * graphviz-java bundles graphviz, a native dependency, accessed via reflection through System.load()
         * we should not minimize it, otherwise the native code gets stripped.
         */
        exclude(dependency('guru.nidi:.*:.*'))
    }
    classifier = ''
}

signing {
    required = isReleaseBuild
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly gradleApi()
    implementation project(":dbml")
    implementation project(":dbml2viz")
    implementation project(":room-consumer")

    testImplementation gradleTestKit()
    testImplementation "junit:junit:4.12"
    testImplementation "com.google.truth:truth:1.0.1"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}