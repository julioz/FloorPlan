import java.nio.file.Paths

plugins {
    id 'application'
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm'
}
apply from: "../versioning.gradle"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

application {
    getMainClass().set("com.zynger.floorplan.FloorPlanCliKt")
}

def embarkVersionFileTask = tasks.register('embarkVersionFile') {
    inputs.property("floorPlanVersion", floorPlanVersion)

    def genDir = layout.buildDirectory.dir('generated-props')
    outputs.dir(genDir)

    doLast {
        genDir.get()
            .file('floorplan-cli-version.properties')
            .asFile
            .text = "floorPlanVersion=$floorPlanVersion"
    }
}

sourceSets {
    main {
        resources {
            srcDir(embarkVersionFileTask)
        }
    }
}

dependencies {
    implementation project(":dbml")
    implementation project(":dbml2viz")
    implementation project(":consumer-sniffer")
    implementation "com.github.ajalt:clikt:2.8.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation "junit:junit:4.12"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
